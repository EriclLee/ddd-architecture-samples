buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'com.google.protobuf' version '0.8.12'
}

apply plugin: 'io.spring.dependency-management'

group = 'study.huhao.demo'
version = '1.0.0'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    implementation 'io.github.lognet:grpc-spring-boot-starter:3.5.3'

    implementation 'com.google.guava:guava:28.2-jre'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'

    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.1'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.1.1'

    implementation 'org.flywaydb:flyway-core'
    testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:6.0.0'

    testImplementation 'io.rest-assured:rest-assured'

    testImplementation 'com.tngtech.archunit:archunit-junit5-api:0.13.1'
    testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:0.13.1'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/protoGen'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.29.0"
        }
    }

    generateProtoTasks {
        ofSourceSet('main').each { task ->
            task.builtins {
                java {
                    outputSubDir = 'protoGen'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'protoGen'
                }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

task cleanProtoGen {
    doFirst {
        delete "$projectDir/src/main/protoGen"
    }
}
clean.dependsOn cleanProtoGen

test {
    useJUnitPlatform()
}
